#!/usr/bin/env zsh

set -euo pipefail

update_links=false
if [[ $#@ -ge 2 ]] && [[ $1 = '--update-links' ]]; then
	update_links=true
	shift
fi

readonly search=${${1:-$PWD}:A}
if [[ -f $search ]]; then
	readonly dirname=${search:h}
else
	readonly dirname=$search
fi

readonly files=$(command rg $search --files --glob '*.md' | command rg '/[0-9]{12,14} ?\.md$')
for file in ${(f)files}
do
	uid=${${file:t}%.md}
	if [[ $#uid -lt 14 ]]; then
		new_uid="${uid:0:12}$(date -r $file '+%S')"
		# trim to 12 chars and append modified seconds
		/usr/bin/sed -i '' -r "s%uid: \[\[$uid\]\]%uid: [[$new_uid]]%g" $file
		uid=$new_uid
	fi
	firstline=$(head -1 "$file")
	if [[ $firstline =~ '^# ' ]]; then
		title=${firstline:2}
		filename="$dirname/$uid $title"
		filename="${filename%% }.md"
		if [[ $file != $filename ]]; then
			mv $file $filename

			if $update_links; then
				${0:A:h}/update-link-titles $filename $uid $title
			fi
		fi
	fi
done
